Module = [{ Decl# | Using | ";" }]
Using = "using" <Str> ";"

Code = ( "{" [{ Line }] "}" ) | Line
Line = If | While | DoWhile | For | Code | Decl# | ( [ ( "return" Value ) | "break" | Value ] ";" )
If = "if" "(" Value ")" Code [ "else" Code ]
While = "while" "(" Value ")" Code
DoWhile = "do" Code "while" "(" Value ")" ";"
For := "for" "(" Decl# | ( [ Value ] ";" ) [ Value ] ";" [ Value ] ")" Code

Value = Comma
AssignValue = Assign
Comma = Assign [{ "," Assign }]
Assign = Ternary [ "=" | "+=" | "-=" | "*=" | "/=" | "^=" Assign ]
Ternary = Bool [ "?" Ternary ":" Ternary ]
Bool = Equality [{ "&&" | "||" | "&" | "|" | "^" Equality }]
Equality = Shift [{ "==" | "!=" | ">" | ">=" | "<" | "<=" Shift }]
Shift = Expr [{ ">>" | "<<" Expr }]
Expr = Term [{ "+" | "-" Term }]
Term = Unary [{ "*" | "/" | "%" Unary }]
Unary = ( "!" | "~" | "-" | "*" | "&" Unary ) | Object [{ "++" | "--" }]
Object = Factor [{   ( "[" Value "]" )
                   | ( "(" [ AssignValue [{ "," AssignValue }] ] ")" )
                   | ( "." | "->" <Ident> ) }]
Factor =   ( "(" Value ")" )
         | ( "(" Type ")" Unary )
         | ( "{" [ AssignValue [{ "," AssignValue }] ] "}" )
         | ( "sizeof" ( "(" Type | Value ")" ) | Unary )
         | <Int> | <Bool> | <Str> | <Ident>

Type = DeclBasic DeclExpr#
Decl = Storage DeclBasic ";" | ( DeclExpr#   ( [{ "," DeclExpr# }] ";" )
									       | Code# )

Field = DeclBasic [ DeclExpr# [{ "," DeclExpr# }] ] ";"
Param = DeclBasic DeclExpr#

Storage = [ "auto" | "static" | "extern" | "typedef" ]
DeclBasic = <Ident> | StructOrUnion | Enum
StructOrUnion = "struct" | "union" Name# [ "{" [{ Field }] "}" ]
Enum = "enum" Name# [ "{" Name# [{ "," Name# }] "}" ]

DeclExpr = DeclUnary [ "=" AssignValue ]
DeclUnary = ( "*" DeclUnary ) | DeclObject
DeclObject = DeclAtom [{ DeclFunction | ( "[" Value "]" ) }]
DeclFunction = "(" [ ( Param [{ "," Param }] [ "," "..." ] ) | "..." ]  ")"
TypeAtom = [ ( "(" DeclExpr ")" ) | Name ]
Name = <UnqualifiedIdent>
