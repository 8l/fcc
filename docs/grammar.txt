Module = [{ ModuleLine }]
ModuleLine = DeclStructOrUnion | ModuleDecl
Code = ( "{" [{ Line }] "}" ) | Line
Line = If | While | DoWhile | For | Code | [ ( "return" Value ) | "break" | SymDef | Value ] ";"
If = "if" "(" Value ")" Code [ "else" Code ]
While = "while" "(" Value ")" Code
DoWhile = "do" Code "while" "(" Value ")" ";"
For := "for" "(" [ SymDef | Value ] ";" [ Value ] ";" [ Value ] ")" Code

Value = Comma
AssignValue = Assign
Comma = Assign [{ "," Assign }]
Assign = Ternary [ "=" | "+=" | "-=" | "*=" | "/=" | "^=" Assign ]
Ternary = Bool [ "?" Ternary ":" Ternary ]
Bool = Equality [{ "&&" | "||" | "&" | "|" | "^" Equality }]
Equality = Shift [{ "==" | "!=" | ">" | ">=" | "<" | "<=" Shift }]
Shift = Expr [{ ">>" | "<<" Expr }]
Expr = Term [{ "+" | "-" Term }]
Term = Unary [{ "*" | "/" | "%" Unary }]
Unary = ( "!" | "~" | "-" | "*" | "&" Unary ) | Object [{ "++" | "--" }]
Object = Factor [{   ( "[" Value "]" )
                   | ( "(" [ AssignValue [{ "," AssignValue }] ] ")" )
                   | ( "." <Ident> )
                   | ( "->" <Ident> ) }]
Factor =   ( "(" Value ")" )
         | ( "(" Type ")" Unary )
         | ( "{" [ AssignValue [{ "," AssignValue }] ] "}" )
         | ( "sizeof" ( "(" Type | Value ")" ) | Value
         | <Int>
         | <Bool>
         | <Str>
         | <Ident>

DeclStructOrUnion = "struct" | "union" Name#
                    [ "{" [{ DeclField ";" }] "}" ] ";"
Type = DeclBasic DeclExpr#
Decl = DeclBasic DeclExpr# [{ "," DeclExpr# }]
ModuleDecl = DeclBasic DeclExpr#   ( "{" Code "}" )
                                 | ( [{ "," DeclExpr# }] ";" )
DeclField = Decl
DeclParam = DeclBasic DeclExpr#

DeclBasic = <Ident>
DeclExpr = DeclUnary [ "=" AssignValue ]
DeclUnary = ( "*" DeclUnary ) | DeclObject
DeclObject = DeclAtom [{ DeclFunction | ( "[" Value "]" ) }]
DeclFunction = "(" [ ( DeclParam [{ "," DeclParam }] [ "," "..." ] ) | "..." ]  ")"
TypeAtom = [ ( "(" DeclExpr ")" ) | Name ]
Name = <UnqualifiedIdent>